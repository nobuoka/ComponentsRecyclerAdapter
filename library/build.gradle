evaluationDependsOn(parent.path)

String libraryVersionStr = ext.libraryVersionStr = '0.2.0-SNAPSHOT'

childProjects.each {
    it.value.afterEvaluate { childProject ->
        childProject.apply plugin: 'com.jfrog.bintray'
        childProject.apply plugin: "maven-publish"

        String artifactIdSuffix = childProject.ext.has('artifactIdSuffix') ?
                "-${childProject.ext.get('artifactIdSuffix')}" : ''
        childProject.publishing {
            publications {
                aarPublication(MavenPublication) {
                    groupId 'info.vividcode.android'
                    artifactId "components-recycler-adapter${artifactIdSuffix}"
                    version libraryVersionStr
                    artifacts = childProject.configurations.archives.allArtifacts

                    // Add dependencies to pom.
                    // See: http://stackoverflow.com/questions/24743562/gradle-not-including-dependencies-in-published-pom-xml
                    pom.withXml {
                        //Creating additional node for dependencies
                        def dependenciesNode = asNode().appendNode('dependencies')

                        childProject.configurations.compile.allDependencies.each {
                            boolean isNotProjectDependency = !(it instanceof ProjectDependency)
                            boolean isNotDataBinding = it.group != 'com.android.databinding'
                            if (it.group != null && it.name != null && isNotProjectDependency && isNotDataBinding) {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                        if (childProject.ext.has('dependenciesForPom')) {
                            childProject.ext.get('dependenciesForPom').each {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                    }
                }
            }
        }

        childProject.bintray {
            user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
            key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
            publications = ['aarPublication']
            pkg {
                repo = 'maven'
                name = 'components-recycler-adapter'
                userOrg = user
                licenses = ['Apache-2.0']
                vcsUrl = 'https://github.com/nobuoka/ComponentsRecyclerAdapter'
                version {
                    name = libraryVersionStr
                    desc = 'Android library'
                    //vcsTag = '1.3.0'
                    //attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
                }
            }
        }

        childProject.bintrayUpload.dependsOn(childProject.assemble)
    }
}
